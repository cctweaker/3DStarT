//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////

void init_wifi()
{
    if (!cfg_wifi || !start_wifi) // WiFi can't start
        return;

    uint8_t i = 0;

    char HOST_NAME[33];
    sprintf(HOST_NAME, "%s_v%.2f_%x", FW_NAME, VERSION, ESP.getChipId());

    WiFi.mode(WIFI_STA);
    WiFi.hostname(HOST_NAME);

    WiFi.begin(SSIDa, PASSa);

    i = 0;
    while (WiFi.status() != WL_CONNECTED && i < 100)
    {
        delay(100);
        i++;
    }

    // if first network not reachable try second one
    if (WiFi.status() != WL_CONNECTED)
    {
        if (strlen(SSIDb) == 0)
            ESP.restart();

        WiFi.begin(SSIDb, PASSb);

        i = 0;
        while (WiFi.status() != WL_CONNECTED)
        {
            delay(100);
            i++;
            if (i > 200)
                ESP.restart();
        }
    }

    use_wifi = true;

    WiFi.scanNetworks(true);
}

//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////

void init_softap()
{
    if (cfg_wifi && start_wifi) // WiFi is configured, no need for softap
        return;

    use_softap = true;

    WiFi.mode(WIFI_AP);

    WiFi.softAPConfig(STA_IP, STA_IP, STA_MASK);

    char STASSID[33];
    sprintf(STASSID, "%s_%06x", FW_NAME, ESP.getChipId());

    WiFi.softAP(STASSID, STA_PASS);

    WiFi.scanNetworks(true);
}

//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////

void init_dns()
{

    if (!use_softap) // WiFi is configured, no need for dns
        return;

    start_webserver = true; // make sure we use webserver
    use_dns = true;

    /* Setup the DNS server redirecting all the domains to the apIP */
    dnsServer.setErrorReplyCode(DNSReplyCode::NoError);
    dnsServer.start(DNS_PORT, "*", STA_IP);
}

//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////

void ota_update()
{
    char url[128];

    sprintf(url, "http://%s?t=%s&v=0.00", update_url, FW_NAME);

    ESPhttpUpdate.update(net, url);

    // ESP.restart();
}

//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////
